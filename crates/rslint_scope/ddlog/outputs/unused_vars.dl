import ast
import vec
import inputs
import config
import scopes
import var_decls
import is_exported
import name_in_scope

output relation UnusedVariables(name: Name, declared: AnyId, span: Span, file: FileId)

// Only hit non-function arguments and non-globals
UnusedVariables(name, declared, span, file) :-
    File(file, _, _, config),
    config.no_unused_vars_enabled(),
    VariableDeclarations(file, name, scope, Some { span }, declared, false, false, _),
    not declared.is_global(),
    not IsExported(file, declared),
    not NameOccursInScope(name, scope, file).

// For names that are function arguments, it doesn't matter if the parent
// function is exported or not
UnusedVariables(name, declared, span, file) :-
    File(file, _, _, config),
    config.no_unused_vars_enabled(),
    VariableDeclarations(file, name, scope, Some { span }, declared, false, true, _),
    not declared.is_global(),
    not NameOccursInScope(name, scope, file).

// Handle globals
UnusedVariables(name, declared, span, file) :-
    File(file, _, _, config),
    config.no_unused_vars_enabled(),
    VariableDeclarations(file, name, scope, Some { span }, declared @ AnyIdGlobal { global }, false, false, _),
    not IsExported(file, declared),
    not NameOccursInScope(name, scope, file).
