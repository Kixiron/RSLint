import ast
import vec
import inputs
import config
import scopes
import var_decls
import is_exported
import name_in_scope

output relation UnusedVariables(name: Name, declared: AnyId, span: Span, file: FileId)

// Only hit non-function arguments and non-globals
UnusedVariables(name, declared, span, file) :-
    File(file, _, _, config),
    config.no_unused_vars_enabled(),

    VariableDeclarations(
        file,
        name,
        scope,
        declared,
        // Ignore function arguments and implict variables
        &VariableMeta {
            .is_function_argument = false,
            .implicitly_declared = false,
            .declaration_span = Some { span },
        },
    ),
    // Ignore global variables
    not declared.is_global(),

    not IsExported(file, declared),
    not NameInScope(file, name, scope.hoisted_scope(), declared).

// For function arguments it doesn't matter if the parent function is exported or not
UnusedVariables(name, declared, span, file) :-
    File(file, _, _, config),
    config.no_unused_vars_enabled(),

    VariableDeclarations(
        file,
        name,
        scope,
        declared,
        &VariableMeta {
            .is_function_argument = true,
            // Ignore implict variables
            .implicitly_declared = false,
            .declaration_span = Some { span },
        },
    ),

    not NameInScope(file, name, scope.unhoisted_scope(), declared).

// Handle globals
UnusedVariables(name, declared, span, file) :-
    File(file, _, _, config),
    config.no_unused_vars_enabled(),

    VariableDeclarations(
        file,
        name,
        scope,
        declared @ AnyIdGlobal,
        // Ignore function arguments and implict variables
        &VariableMeta {
            .is_function_argument = false,
            .implicitly_declared = false,
            .declaration_span = Some { span },
        },
    ),
    
    not IsExported(file, declared),
    not NameInScope(file, name, scope.unhoisted_scope(), declared).
