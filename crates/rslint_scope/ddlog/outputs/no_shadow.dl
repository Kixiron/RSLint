import ast
import vec
import utils
import scopes
import inputs
import config
import var_decls
import name_in_scope

output relation NoShadow(
    variable: Name,
    original: (AnyId, Span),
    shadower: (AnyId, Span),
    implicit: bool,
    file: FileId,
)

NoShadow(name, (shadowed_id, shadowed_span), (shadower_id, shadower_span), false, file) :-
    File(file, _, _, config),
    config.no_shadow_enabled(),

    // Get the initial variable
    ScopeOfDecl(file, shadowed_scope, shadowed_id),
    VariableDeclarations(
        file,
        name,
        shadowed_scope_raw,
        shadowed_id,
        &VariableMeta {
            .implicitly_declared = false,
            .declaration_span = Some { shadowed_span },
        },
    ),

    // Search the child scopes for visible declarations of the same name
    DeclarationInDescendent(file, shadowed_scope, name, shadower_id),

    // A declaration cannot shadow itself
    shadowed_id != shadower_id,

    // Filter declarations for one with an identical name
    VariableDeclarations(
        file,
        name,
        shadower_scope_raw,
        shadower_id,
        &VariableMeta {
            .implicitly_declared = false,
            .declaration_span = Some { shadower_span },
        },
    ),

    // Within this match all "first", "second", etc. positional references are
    // in lexical terms, where the first declaration occurs before the second
    // in the physical source file
    match ((config.no_shadow_hoisting(), shadower_scope_raw.is_hoistable(), shadowed_scope_raw.is_hoistable())) {
        // If a variable is hoisted, the second shadows the first
        (true, true, true) -> shadower_span < shadowed_span,
        (true, false, true) -> shadower_span < shadowed_span,
        (true, true, false) -> shadower_span < shadowed_span,

        // Otherwise this is a valid shadowing
        (_, _, _) -> true,
    }.


relation DeclarationInDescendent(file: FileId, scope: ScopeId, name: Name, id: AnyId)

DeclarationInDescendent(file, scope, name, id) :-
    ScopeOfDecl(file, scope, id),
    VariableDeclarations(file, name, _, id, _).

DeclarationInDescendent(file, parent, name, id) :-
    DeclarationInDescendent(file, child, name, id),
    InputScope(parent, child, file).


relation ScopeOfDecl(file: FileId, scope: ScopeId, declared: AnyId)

ScopeOfDecl(file, scope, declared) :-
    File(file, _, _, config),
    config.no_shadow_enabled(),

    VariableDeclarations(file, _, Unhoistable { scope }, declared, _),
    not declared.is_global().

ScopeOfDecl(file, scope, declared) :-
    File(file, _, _, config),
    config.no_shadow_enabled(),

    VariableDeclarations(file, _, Hoistable { hoisted_scope, unhoisted_scope }, declared, _),
    not declared.is_global(),
    var scope = if (config.no_shadow_hoisting()) { hoisted_scope } else { unhoisted_scope }.
